plugins {
    id 'com.widen.versioning' version '0.3.3'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'com.widen.oss'

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'com.widen.versioning'

    group = 'com.widen.oss'
    ext.repo = 'https://github.com/Widen/tabitha'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.2'
        annotationProcessor 'org.projectlombok:lombok:1.18.2'

        compileOnly 'org.atteo.classindex:classindex:3.4'
        annotationProcessor 'org.atteo.classindex:classindex:3.4'

        testCompile 'cglib:cglib-nodep:3.2.5'
        testCompile 'org.codehaus.groovy:groovy-all:2.4.9'
        testCompile 'org.objenesis:objenesis:2.6'
        testCompile 'org.slf4j:slf4j-simple:1.7.25'
        testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier 'javadoc'
    }
}

configure([project(':tabitha-core'), project(':tabitha-delimited'), project(':tabitha-excel'), project(':tabitha-json')]) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = project.name
                    description = provider {
                        project.description
                    }
                    url = project.ext.repo
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                    organization {
                        name = 'Widen Enterprises, Inc'
                        url = 'https://www.widen.com'
                    }
                    developers {
                        developer {
                            id = 'scoakley'
                            name = 'Stephen Coakley'
                            email = 'scoakley@widen.com'
                        }
                    }
                    scm {
                        url = project.ext.repo + '.git'
                        connection = 'scm:git@github.com:Widen/tabitha.git'
                        developerConnection = 'scm:git@github.com:Widen/tabitha.git'
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
            findProperty('mavenCentralSigningKey') ?: System.getenv('MAVEN_CENTRAL_SIGNING_KEY'),
            findProperty('mavenCentralSigningPassword') ?: System.getenv('MAVEN_CENTRAL_SIGNING_PASSWORD')
        )
        sign publishing.publications.maven
    }
}
