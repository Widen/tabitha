plugins {
    id 'com.jfrog.bintray' version '1.8.4' apply false
    id 'com.widen.versioning' version '0.3.3'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.widen.versioning'

    group = 'com.widen'
    ext.repo = 'https://github.com/Widen/tabitha'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.2'
        annotationProcessor 'org.projectlombok:lombok:1.18.2'

        compileOnly 'org.atteo.classindex:classindex:3.4'
        annotationProcessor 'org.atteo.classindex:classindex:3.4'

        testCompile 'cglib:cglib-nodep:3.2.5'
        testCompile 'org.codehaus.groovy:groovy-all:2.4.9'
        testCompile 'org.objenesis:objenesis:2.6'
        testCompile 'org.slf4j:slf4j-simple:1.7.25'
        testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    }

    bintray {
        user = findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
        key = findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_KEY')
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier 'javadoc'
    }

    bintray {
        publications = ['maven']
        publish = true
        dryRun = true
        pkg {
            repo = 'oss'
            name = project.name
            userOrg = 'widen'
            desc = project.description
            websiteUrl = project.ext.repo
            issueTrackerUrl = project.ext.repo + '/issues'
            vcsUrl = project.ext.repo + '.git'
            licenses = ['MIT']
            publicDownloadNumbers = true
            version {
                name = project.version
                gpg {
                    sign = true
                }
            }
        }
    }

    bintrayUpload.doFirst {
        bintrayUpload.versionName = project.version
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = project.name
                    description = project.description
                    url = project.ext.repo
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'scoakley'
                            name = 'Stephen Coakley'
                            email = 'scoakley@widen.com'
                        }
                    }
                    scm {
                        url = project.ext.repo + '.git'
                        connection = 'scm:git@github.com:Widen/tabitha.git'
                        developerConnection = 'scm:git@github.com:Widen/tabitha.git'
                    }
                }
            }
        }
    }
}
